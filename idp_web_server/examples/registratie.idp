vocabulary environment {
    type Location ≜ {Flemish_Region, Walloon_Region, Brussels_Region}
    type Real_Estate_Type ≜ {Residential, Residential_Building_Land, Other_Real_Estate}
    type Destination ≜ {As_Family_Dwelling, For_Resale, Community_Purpose, Social_Housing, Social_Renting, Other_House, No_House}
    type Buyer_Type ≜ {Natural_Person, Legal_Person, Mixed}
    type Year ≜ {2015..2025}
    type Agreement_Dwelling_For_Social_Renting ≜ {Less_Than_9_years, More_than_9_Or_9_years}
    type Transaction ≜ {Public_Sale, Out_Hand, Public_Sale_Community}
    type Resale ≜ {Less_Than_Three, Three_Or_More}
    type Seller ≜ {Vlaams_Woningfonds, Gelijksoortige_Rechtspersoon_EER, Mpy_Erkend_Door_VMSW, Vlaamse_Mpy_Voor_Sociaal_Wonen, Erkende_Sociale_Huisvestings_Mpy}

    [EN:the region] region : () → Location


    // for Social_Dwelling
    [EN:the buyer] buyer : () → Buyer_Type

    [EN:the value of the real estate] value: () → ℝ

    [EN:the seller] seller : () → Seller
    mortgage_loan_allowance : () → 𝔹

    // for Heritage
    [EN:the type of real estate] type_of_real_estate : () → Real_Estate_Type
    [EN:the intended use] use : () → Destination
    [EN:it is a monument] monument : () → 𝔹
    [EN:it is an heritage investment] heritage_investment : () → 𝔹
    [EN:it is part of an heritage plan] heritage_plan : () → 𝔹
    [EN:additional terms are satisfied] additional_terms : () → 𝔹

    // For Professional_Purchase
    [EN:the buyer is professional] professional_buyer : () → 𝔹
    [EN:a professional declaration has been filed] professional_declaration_filed : () → 𝔹
    [EN:the guarantee is satisfied] guarantee_satisfied : () → 𝔹
    [EN:additional security is required] additional_security_required : () → 𝔹
    [EN:additional security is provided] additional_security_satisfied : () → 𝔹
    [EN:the number of resales] resale : () → Resale
    [EN:the transaction type] transaction_type : () → Transaction
    [EN:the date of certification of the sale] certification_date_sale : () → Year

    // For Energy_Efficient_Dwelling
    [EN:it will be the domicile of the buyer] domicile_established : () → 𝔹
    [EN:it is a clean purchase] clean_purchase : () → 𝔹
    [EN:it is the first ownership of the buyer] first_ownership : () → 𝔹
    [EN:there is a causal connection] causal_connection : () → 𝔹
    [EN:energy-saving renovation is planned] energy_renovation_planned : () → 𝔹
    [EN:the term of the energy-saving renovation] term_energy_renovation : () → Year
    [EN:the date of certification] certification_date : () → Year

    // For Dwelling_For_Social_Renting
    [EN:the date the social renting starts] start_dt_dwelling_for_social_rent : () → Year
    [EN:the duration of the agreement] duration_of_agreement : () → Agreement_Dwelling_For_Social_Renting

}

vocabulary decision {
    import environment

    type Registration_type ≜ {Family_Dwelling, Social_Dwelling, Social_Purpose, Exchange_Unbuilt_Ground, Professional_Purchase, Energy_Efficient_Dwelling, Heritage, Dwelling_For_Social_Renting, Other}
    type Tax_Rate ≜ {1, 2, 4, 6, 7, 10, 12}

    [EN:the registration type] registration_type : () → Registration_type
    [EN:the tax rate] tax_rate : () → Tax_Rate
    [EN:the tax amount] tax_amount : () → ℝ
}

theory environment:environment {
    { professional_buyer() if
        professional_declaration_filed()
        and guarantee_satisfied()
        and (additional_security_required() and additional_security_satisfied()
            or ¬additional_security_required())
        and resale() is Three_Or_More.
    }
}

theory decision:decision {

  [Tax rate legislation]
    {
        [1% tax for Heritage in Flemish region]
        tax_rate() is 1 if registration_type() is Heritage
                         and region() is Flemish_Region.

        // Correct amount is 1.5
        [2% tax for Social Dwellings in Flemish region]
        tax_rate() is 2 if registration_type() is Social_Dwelling
                         and region() is Flemish_Region.

        [4% tax for Professional buyers in Flemish region]
        tax_rate() is 4 if registration_type() is Professional_Purchase
                         and region() is Flemish_Region.

        [6% tax for Energy efficient dwellings, social purpose or exchange unbuilt ground in Flemish region]
        tax_rate() is 6 if (registration_type() is Energy_Efficient_Dwelling
                          or registration_type() is Social_Purpose
                          or registration_type() is Exchange_Unbuilt_Ground)
                         and region() is Flemish_Region.

        [7% tax for family dwelling or dwelling for social renting in Flemish region]
        tax_rate() is 7 if (registration_type() is Family_Dwelling
                           or registration_type() is Dwelling_For_Social_Renting)
                          and region() is Flemish_Region.

        [10% default tax in Flemish region]
        tax_rate() is 10 if region() is Flemish_Region
                          and registration_type() is Other.

        [12% tax ∈ Brussels and Walloon region]
        tax_rate() is 12 if region() is Brussels_Region
                          or region() is Walloon_Region.
  }

    // registration types currently only defined for Flanders.
    // When a choice is made for walonia or brussels, types can still be entered with a view to expandability

    [An Heritage is a residential monument in an heritage plan (with additional terms), used as family dwelling]
      ((type_of_real_estate() is Residential
      and monument()
      and use() is As_Family_Dwelling
      and heritage_investment()
      and heritage_plan()
      and additional_terms())
    are necessary conditions for
      (registration_type() is Heritage)).

    [A social dwelling is a residential real estate sold by Vlaamse_Mpy_Voor_Sociaal_Wonen or Erkende_Sociale_Huisvestings_Mpy to a natural person for social housing with a mortgage allowance]
      ((buyer() is Natural_Person
      and type_of_real_estate() is Residential
      and use() is Social_Housing
      and (seller() is Vlaamse_Mpy_Voor_Sociaal_Wonen or seller() is Erkende_Sociale_Huisvestings_Mpy)
      and mortgage_loan_allowance())
    are necessary conditions for
      (registration_type() is Social_Dwelling)).

    [A Professional purchase is a non-public sale to a professional buyer for resale with a certification not older than 8 years]
      ((professional_buyer()
      and transaction_type() is not Public_Sale
      and use() is For_Resale
      and certification_date_sale() is certification_date() + 8)
    are necessary conditions for
      (registration_type() is Professional_Purchase)).

    [Rule for Energy Efficient Dwelling]
      ((type_of_real_estate() is Residential
      and buyer() is Natural_Person
      and domicile_established()
      and clean_purchase()
      and (first_ownership() or causal_connection())
      and energy_renovation_planned()
      and term_energy_renovation() is certification_date() + 5)
    are necessary conditions for
      (registration_type() is Energy_Efficient_Dwelling)).

    [A sale by the Vlaams Woning Fonds for community purpose is for social purpose.]
      ((seller() in {Mpy_Erkend_Door_VMSW,
                  Vlaams_Woningfonds,
                  Gelijksoortige_Rechtspersoon_EER}
      and use() is Community_Purpose)
    are necessary conditions for
      (registration_type() is Social_Purpose)).

    [Rule for Exchange Unbuilt Ground]
      ((additional_terms()
      and ¬monument())
    are necessary conditions for
      (registration_type() is Exchange_Unbuilt_Ground)).

    [Rule for Family Dwelling]
      ((type_of_real_estate() is Residential
      and buyer() is Natural_Person
      and domicile_established()
      and clean_purchase()
      and (first_ownership() or causal_connection())
      and (¬energy_renovation_planned() or term_energy_renovation() is not certification_date() + 5))
    are necessary conditions for
      (registration_type() is Family_Dwelling)).

    [Rule for social renting]
      ((type_of_real_estate() is Residential
      and use() is Social_Renting
      and start_dt_dwelling_for_social_rent() is certification_date() + 3
      and duration_of_agreement() is Less_Than_9_years)
    are necessary conditions for
      (registration_type() is Dwelling_For_Social_Renting)).

    [Tax amount calculation]
    tax_amount() is value() ⨯ tax_rate() / 100.

}
structure default:environment {
    region ≜ Flemish_Region.
}
display {
    goal_symbol ≜ {`tax_amount}.
    view() is normal.
    expand ≜ {`registration_type, `tax_rate}.
    heading(Building, `region, `type_of_real_estate, `monument).
    heading(Buyer, `buyer, `domicile_established, `first_ownership,
      `causal_connection, `professional_buyer, `professional_declaration_filed,
      `guarantee_satisfied, `additional_security_required,
      `additional_security_satisfied).
    heading('Sales Transaction', `seller, `value, `transaction_type, `use, `resale,
      `heritage_investment, `heritage_plan, `additional_terms,
      `mortgage_loan_allowance, `certification_date, `certification_date_sale, `clean_purchase,
      `energy_renovation_planned, `term_energy_renovation, `start_dt_dwelling_for_social_rent,
      `duration_of_agreement).
    heading(Registration, `registration_type, `tax_rate, `tax_amount).
    unit('%', `tax_rate).
}
