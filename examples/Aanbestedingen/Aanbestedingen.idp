vocabulary V {
    type score isa int

    type PublicatieTypes constructed from {
        Open,
        Beperkt,
        Nvt
    }

    type AantalSelectiefazenTypes constructed from {
        InEenStap,
		InTweeStappen,
        Ntv
    }

    type PublicatieTermijnen constructed from {
		Offerte_22,
        Offerte_36,
        Offerte_52,
        Deelneming_15Offerte_15Tot_30,
        Deelneming_37Offerte_40Tot_77,
        Geen
    }

    type PublicatieTerritorialiteiten constructed from {
        Europees,
        Nationaal,
        Nvt
    }

    type AardPrestatie constructed from {
        Werk,
        Dienst,
        Levering
    }

    type GunningsCriteriaTypes constructed from {
        MagAlleenPrijsZijn,
        PrijsEnAndere,
        PrijsOfAndere
    }

    type GunningsprocedureTypes constructed from {
        Aanbesteding,
        Offerteaanvraag,
        OnderhandelingsProcedure,
        ConcurrentieDialoog
    }

    type GunningsprocedurePlusTypes constructed from {
        Aanbesteding,
        Offerteaanvraag,
        OnderhandelingsProcedure,
        ConcurrentieDialoog,
        Geen
    }

    type OnderhandelingsProcedureTypes constructed from {
        MetVoorafgaandeBekendmaking,
        ZonderVoorafgaandeBekendmaking,
        Vereenvoudigd,
        Nvt
    }

    type ResultaatVanProcedureTypes constructed from {
        GeenAanvraagTotDeelname,
        GeenGeschikteAanvraagTotDeelname,
        GeenOffertes,
        GeenGeschikteOffertes,
        AlleenOnregelmatigeOffertes,
        AlleenOnaanvaardbareOffertes,
        ErWarenKandidaat
    }

    type GoedTeKeurenGrenzen constructed from {
        KleinerOfGelijkAan_8500,
        KleinerOfGelijkAan_85000,
		HogerDan_85000,
        Nvt
    }

	// nog aan te vullen
    type RamingsGrenzen constructed from {
        KleinerDan_600000,
		HogerOfGelijkAan_600000,
        KleinerOfGelijkAan_5MioEuro,
        HogerDan_5MioEuro,
        Nvt
    }

    type AanvullingsBedragGrenzen constructed from {
        KleinerOfGelijkAan_50Prc,
        GroterDan_50Prc,
        Nvt
    }
	
    DoorlooptermijnEvalutatie : AantalSelectiefazenTypes

    VoorafgaandeProcedure : GunningsprocedurePlusTypes
    ResultaatVorigeProcedure : ResultaatVanProcedureTypes

    AardVanDePrestatie : AardPrestatie

    Gunningsprocedure : GunningsprocedureTypes
    GunningsCriteria : GunningsCriteriaTypes

    GeheimeOpdracht
    HoogdringendeOpdracht
    TechnischeOfArtistiekeSpecificiteit
	
	IsErEenOpdrachtInUitvoering
	AanvullendeOpdracht
	HerhalingOpdrachtVoorzien
	SluitingOpdrachtBinnenDe3Jaar
	DezelfdeOpdracht
	GelijkaardigeOpdracht
	OnvoorzieneOmstandigheden
	ComplexeOpdracht
	OnzekerOnstandigheden
	PrijsNietVaststelbaar
	SocialeInschakelingsondernemingen
	BeschutteWerkplaatsen
	
	OnmogelijkBehoefteTeBepalen
	OnmogelijkBeoordelenAanbodMarktFinancieel
	OnmogelijkBeoordelenAanbodMarktJuridisch
	OnmogelijkBeoordelenAanbodMarktTechnisch
	StandaardProcedureIsNietToeTePassen
	
    AanvullendeOpdrachtWasTechnischOfEconomischScheidbaar
    AanvullendeOpdrachtStriktNoodzakelijk
	BedragAanvulling : AanvullingsBedragGrenzen
    GunningAanOorsponkelijkeAannemer
	
	VerruimingConcurrentieToegelaten

	OnderhandelingsProcedureType : OnderhandelingsProcedureTypes

    GoedTeKeurenBedrag : GoedTeKeurenGrenzen

    RamingOpdracht : RamingsGrenzen
    PublicatieType : PublicatieTypes
    PublicatieTermijnKd : PublicatieTermijnen
    PublicatieTerritorialiteit : PublicatieTerritorialiteiten
    PublicatieInkortbaar

    OnderhandelingsMarge
    StandaardProcedure
    UitzonderingsProcedure
    VoorwaardenNietWezelijkGewijzigd
	VerderGaanMetFormeelRegelmatigeAanbieders
    VerderGaanMetGeselecteerdeAanbieders
	
	KennisgevingEuropeseCommissie

    // Intermediate predicates
    _PT(PublicatieTermijnen)
    _GK(GunningsCriteriaTypes)


    /*
        type T isa int
        vantoep(RealTup)
    */

    // 171, ..., 175
    type ParagraafNr = {1..2} isa nat

    // 117, ..., 517
    type SectieNr = {1..2} isa nat
	
    type ArtikelNr = { 26..27} isa nat

    type SubSectie constructed from {a, b, c, d, e, f}

    type TheoretischArtikel constructed from {Artikel(ArtikelNr, ParagraafNr, SectieNr, SubSectie)}
    type WetsArtikel isa TheoretischArtikel
    
    ArtikelVanToepassing(WetsArtikel)

    SectieVanToepassing(ArtikelNr, ParagraafNr, SectieNr)

    ParagraafVanToepassing(ArtikelNr, ParagraafNr)
}

theory T : V {

    StandaardProcedure <=>
        	Gunningsprocedure = Offerteaanvraag |
        	Gunningsprocedure = Aanbesteding.

    UitzonderingsProcedure <=>
        	Gunningsprocedure = OnderhandelingsProcedure |
    		Gunningsprocedure = ConcurrentieDialoog.

    // bedoeld om de af te leiden welke marge een gunningswijze heeft
    OnderhandelingsMarge <=>
        	Gunningsprocedure = OnderhandelingsProcedure |
        	Gunningsprocedure = ConcurrentieDialoog.

    _GK(GunningsCriteria).

    {
        _GK( MagAlleenPrijsZijn ) <-
        	Gunningsprocedure = Aanbesteding.		
        _GK( PrijsEnAndere ) <-
        	Gunningsprocedure = Offerteaanvraag.		
        _GK( PrijsOfAndere ) <-
        	Gunningsprocedure = OnderhandelingsProcedure |
        	Gunningsprocedure = ConcurrentieDialoog.		
    }

	StandaardProcedure =>
    	OnderhandelingsProcedureType = Nvt[:OnderhandelingsProcedureTypes] &
    	PublicatieType ~= Nvt[:PublicatieTypes].

	UitzonderingsProcedure =>
    	OnderhandelingsProcedureType ~= Nvt[:OnderhandelingsProcedureTypes] &
    	PublicatieType = Nvt[:PublicatieTypes].

    PublicatieType = Open <=> DoorlooptermijnEvalutatie = InEenStap.
    PublicatieType = Beperkt <=> DoorlooptermijnEvalutatie = InTweeStappen.

    _PT(PublicatieTermijnKd).
    {
        _PT(Offerte_22) <-
        	OnderhandelingsProcedureType = Vereenvoudigd.
        _PT(Offerte_36) <-
        	PublicatieTerritorialiteit = Nationaal &
        	PublicatieType = Open.
        _PT(Offerte_52) <-
        	PublicatieTerritorialiteit = Europees &
        	PublicatieType = Open.
        _PT(Deelneming_15Offerte_15Tot_30) <-
        	PublicatieTerritorialiteit = Nationaal &
        	PublicatieType = Beperkt.
        _PT(Deelneming_37Offerte_40Tot_77) <-
        	PublicatieTerritorialiteit = Europees &
        	PublicatieType = Beperkt.
        _PT(Geen) <-
        	PublicatieType = Nvt[:PublicatieTypes].
    }

    RamingOpdracht = HogerDan_5MioEuro =>
    	PublicatieTerritorialiteit = Europees.

    // Een sectie is van toepassing als en slechts als tenminste 殚n van zijn wetten van toepassing is.
    ! x y t : SectieVanToepassing(t, x, y) <=> ? z : ArtikelVanToepassing(Artikel(t, x, y, z)).

    // Een paragraaf is van toepassing als en slechts als tenminste 殚n van zijn secties van toepassing is.

    ! x t : ParagraafVanToepassing(t, x) <=> ? y : SectieVanToepassing(t, x, y).

	// art 26 171 wet -> ZonderVoorafgaandeBekendmaking
    OnderhandelingsProcedureType = ZonderVoorafgaandeBekendmaking =>
    	ParagraafVanToepassing(26, 1).

	// art 26 171 117 a wet -> maximum bedrag
    ArtikelVanToepassing(Artikel(26, 1, 1, a)) =>
    	(
    		GoedTeKeurenBedrag = KleinerOfGelijkAan_85000 |
    	  	GoedTeKeurenBedrag = KleinerOfGelijkAan_8500
    	).

	// art 26 171 117 b wet
    ArtikelVanToepassing(Artikel(26, 1, 1, b)) =>
 		GeheimeOpdracht.

	// art 26 171 117 c wet
    ArtikelVanToepassing(Artikel(26, 1, 1, c)) =>
    	HoogdringendeOpdracht.

	// art 26 171 117 d wet -> Geen aanvragen of offertes
    ArtikelVanToepassing(Artikel(26, 1, 1, d)) =>
        (
    		(
    			VoorafgaandeProcedure = Aanbesteding |
        		VoorafgaandeProcedure = Offerteaanvraag
        	) &
        	(
        		ResultaatVorigeProcedure = GeenAanvraagTotDeelname |
        		ResultaatVorigeProcedure = GeenGeschikteAanvraagTotDeelname |
        		ResultaatVorigeProcedure = GeenOffertes |
        		ResultaatVorigeProcedure = GeenGeschikteOffertes
        	) &
        	VoorwaardenNietWezelijkGewijzigd &
			(
				PublicatieTerritorialiteit ~= Europees |
				KennisgevingEuropeseCommissie			
			)
    	).

	// art 26 171 117 e wet -> Offerteproblemen
    ArtikelVanToepassing(Artikel(26, 1, 1, e)) =>
    	(
    		(
    			VoorafgaandeProcedure = Aanbesteding |
        		VoorafgaandeProcedure = Offerteaanvraag |
        		VoorafgaandeProcedure = ConcurrentieDialoog
        	) &
        	(
        		ResultaatVorigeProcedure = AlleenOnregelmatigeOffertes |
        		ResultaatVorigeProcedure = AlleenOnaanvaardbareOffertes
        	) &
        	VoorwaardenNietWezelijkGewijzigd &
       		VerderGaanMetFormeelRegelmatigeAanbieders &
        	VerderGaanMetGeselecteerdeAanbieders
    	).

	// art 26 171 117 f wet
    ArtikelVanToepassing(Artikel(26, 1, 1, f)) =>
    	TechnischeOfArtistiekeSpecificiteit.

	// art 26 171 217 a wet -> aanvullende werken
    ArtikelVanToepassing(Artikel(26, 1, 2, a)) =>
  		(
    		AanvullendeOpdracht &
    		GunningAanOorsponkelijkeAannemer &
    		BedragAanvulling = KleinerOfGelijkAan_50Prc &
			OnvoorzieneOmstandigheden &
    		( ~ AanvullendeOpdrachtWasTechnischOfEconomischScheidbaar |
    		  AanvullendeOpdrachtStriktNoodzakelijk
    		)
    	).

	// art 26 171 217 b wet -> Herhaling van opdracht
    ArtikelVanToepassing(Artikel(26, 1, 2, b)) =>
  		(
			IsErEenOpdrachtInUitvoering &
			(
				DezelfdeOpdracht |
				GelijkaardigeOpdracht
			) &
			HerhalingOpdrachtVoorzien &
			SluitingOpdrachtBinnenDe3Jaar
		).

	AanvullendeOpdracht => IsErEenOpdrachtInUitvoering.
	HerhalingOpdrachtVoorzien => IsErEenOpdrachtInUitvoering.
	SluitingOpdrachtBinnenDe3Jaar => IsErEenOpdrachtInUitvoering.
	DezelfdeOpdracht => IsErEenOpdrachtInUitvoering.
	GelijkaardigeOpdracht => IsErEenOpdrachtInUitvoering.
    OnvoorzieneOmstandigheden => AanvullendeOpdracht.
    AanvullendeOpdrachtWasTechnischOfEconomischScheidbaar => AanvullendeOpdracht.
    AanvullendeOpdrachtStriktNoodzakelijk => AanvullendeOpdracht.

    {
		VerruimingConcurrentieToegelaten <-
			OnderhandelingsProcedureType = ZonderVoorafgaandeBekendmaking &
			RamingOpdracht ~= HogerDan_5MioEuro.
	}	
	
	
	// HENDRIK KIJKT NA !! -> Vereenvoudigd
	// art 26 172 -> MetVoorafgaandeBekendmaking
    OnderhandelingsProcedureType = MetVoorafgaandeBekendmaking =>
		SectieVanToepassing(26, 2, 1).

	// art 26 172 117 a wet -> Offerteproblemen
    ArtikelVanToepassing(Artikel(26, 2, 1, a)) =>
  		(
			VoorafgaandeProcedure = Aanbesteding |
			VoorafgaandeProcedure = Offerteaanvraag |
			VoorafgaandeProcedure = ConcurrentieDialoog
        ) &
        (
			ResultaatVorigeProcedure = AlleenOnregelmatigeOffertes |
			ResultaatVorigeProcedure = AlleenOnaanvaardbareOffertes
        ) &
        VoorwaardenNietWezelijkGewijzigd &
        ~ VerderGaanMetFormeelRegelmatigeAanbieders &
    	~ VerderGaanMetGeselecteerdeAanbieders
	.

	// art 26 172 117 b wet -> OndefinieerbareOpdrachten
    ArtikelVanToepassing(Artikel(26, 2, 1, b)) =>
		(
			ComplexeOpdracht |
			OnzekerOnstandigheden
		) &
		PrijsNietVaststelbaar
	.

	// art 26 172 117 c wet -> VoorbehoudenOpdrachten
    ArtikelVanToepassing(Artikel(26, 2, 1, c)) =>
		(
			SocialeInschakelingsondernemingen |
			BeschutteWerkplaatsen
		) &
		PublicatieTerritorialiteit ~= Europees
	.

	// art 26 172 117 d wet -> onderhandelingsprocedure
    ArtikelVanToepassing(Artikel(26, 2, 1, d)) =>
		RamingOpdracht = KleinerDan_600000
	.

	// art 26 172 217 wet -> research & Dev
    ~SectieVanToepassing(26, 2, 2).

    OnderhandelingsProcedureType = Nvt[:OnderhandelingsProcedureTypes] <=>
    	Gunningsprocedure ~= OnderhandelingsProcedure.

	// art 27 wet -> concurrentiedialoog
    Gunningsprocedure = ConcurrentieDialoog =>
    	(
			OnmogelijkBehoefteTeBepalen |
			OnmogelijkBeoordelenAanbodMarktTechnisch |
			OnmogelijkBeoordelenAanbodMarktJuridisch |
			OnmogelijkBeoordelenAanbodMarktFinancieel
		) &
		StandaardProcedureIsNietToeTePassen
	.
	
	StandaardProcedure <=> ~ StandaardProcedureIsNietToeTePassen.
	
	// Inhoud/Modaliteiten opdracht
	

	// ??? nu altijd artikels a -f  maar die bestaan net altijd -> hoe wegwerken ?
	// ??? wettelijk termen gebruiken ipv short versions
	
}

structure S : V {
    AardVanDePrestatie = Werk
    
    // reele WetsArtikels
    WetsArtikel = {
        Artikel(26, 1, 1, a);
        Artikel(26, 1, 1, b);
        Artikel(26, 1, 1, c);
        Artikel(26, 1, 1, d);
        Artikel(26, 1, 1, e);
        Artikel(26, 1, 1, f);
        Artikel(26, 1, 2, a);
        Artikel(26, 1, 2, b);
        Artikel(26, 2, 1, a);
        Artikel(26, 2, 1, b);
        Artikel(26, 2, 1, c);
        Artikel(26, 2, 1, d)
    }
}

procedure main() {
    stdoptions.nbmodels = 10
    printmodels(modelexpand(T,S))
    printcore(T,S)
}
